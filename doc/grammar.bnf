// [] optional
// ? zero or one
// +  One or more
// * zero or more
// () Grouping
// |  Or

program ::=  block_body

block_body ::= expression+ | statement*


// Expressions
expression ::= block_invocation
             | method_invocation
             | "(" expression ")"
             | literal
             | literal_non_word_invocation
             | array_access
             | dictionary_access
// Statements
statement ::= variable_definition
    | variable_declaration
    | block_declaration ["=" block_literal]
    | block_definition
    | "return" expression*
    | "continue"
    | "break"


// Declaration
variable_declaration ::=
     variable [type] // optional in inferrable generics
    | variable_with_info_string
    | variable array_declaration
    | variable dictionary_declaration

block_declaration ::= type_identifier " {" (variable type)* "}"
// Array decl
array_declaration ::= "[]" type
                    | "[]" // generic array
// Dictionary decl
dictionary_declaration ::= "[" type ":" type "]"
                         | "[:]" // generic dictionary

// Definitions
block_definition ::= type_identifier ":" block_literal
variable_definition  ::= variable ":" (expression)+

// Literals
literal ::= block_literal
            | number_literal
            | string_literal
            | array_literal
            | dictionary_literal

block_literal ::= "{" block_body "}"
number_literal ::= ('0-9')+ | "-"("0-9")+
string_literal ::= "\"" PRINTABLE* "\""
                  | "'" PRINTABLE*  "'"
                  | "`" PRINTABLE* "`"
info_string ::= string_literal variable
variable_with_info_string ::= info_string

array_literal ::= "[" expression* "]" // constraint, expression should be of the same type
dictionary_literal ::= "[" ( expression ":" expression )* "]"

// Array access
array_access ::= array_read | array_write
array_read ::= identifier "[" expression "]"
array_write ::= identifier "[" expression "]" "=" expression

// Dictionary access
dictionary_access ::= dictionary_read | dictionary_write
dictionary_read ::=  identifier "[" expression "]"
dictionary_write ::= identifier "[" expression ":" expression "]"



// member access
member_access ::= variable ("." variable)+
// Invocation
block_invocation ::=  variable invocation_args
                     | variable named_invocation_args

method_invocation ::= member_access block_invocation
                    | member_access non_word_invocation
non_word_invocation ::= member_access punctuation_identifier invocation_args
literal_non_word_invocation ::= literal punctuation_identifier invocation_args


invocation_args ::= "(" ")"
                | "(" (expression)+ ")"
                |  (expression)+
named_invocation_args ::= "(" (identifier ":" expression)* ")"

// Identifiers
identifier ::= variable_identifier | type_identifier
type ::= type_identifier
variable::= variable_identifier
type_identifier ::= UPPER_CASE (variable_identifier)*
variable_identifier ::= CHARACTER+ // not start with numbers, don't contain reserved and don't start with uppercase
punctuation_identifier ::= PUNCTUATION+// as defined by unicode, minus reserved punct
word_identifier ::= CHARACTER+ //
reserved_char ::= "{"|"}"|"["|"]"|":"|"//"|","|"."
